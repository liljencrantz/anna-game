use(annaGame.scene);
use(actor);
use(linalg);

class Stepable
{
    def Object step(Float dt){};
}

class World
{
    var Scene scene;
    
    var MutableList«Actor» actor;
    var MutableList«Stepable» stepable;
    var Actor _player;
    var Float time;
    
    def __init__(Scene scene)
    {
	this.scene = scene;
	actor = «Actor»[];
	stepable = «Stepable»[];
	time = 0.0;
    }
    
    def playerGetter()
    {
	_player;
    }
    
    def playerSetter(Actor player)
    {
	_player = player;
    }
    
    var Actor player (property(playerGetter, playerSetter));

    def add(Object o)
    {
	expandCode(
	    if(o as %Type)
	    {
		%list.push(o as %Type);
	    }
	    ,
	    Type: [Actor, Stepable],
	    list: [actor, stepable]);
    }
    
    def step(Float dt)
    {
	time += dt;
	stepable.each(it)
	{
	    it.step(dt);
	}
    }

    def cameraSetter(Vec val)
    {
	scene.cameraX = val.x;
	scene.cameraY = val.y;
	scene.cameraZ = val.z;
    }

    def cameraGetter()
    {
	Vec(scene.cameraX, scene.cameraY, scene.cameraZ)
    }

    var Vec camera (property(cameraGetter, cameraSetter));

}

